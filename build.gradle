plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id "com.moowork.node" version "1.2.0"
    id 'application'
    id 'distribution'
}

group 'org.jetbrains'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    ivy {
        File baseDir = new File(project.projectDir, "lib")
        url = baseDir
        artifactPattern("${baseDir.canonicalPath}/[organisation]-[artifact]-[revision](-[classifier]).jar")
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile group: 'org.apache.commons', name: 'commons-vfs2', version: '2.2'
    compile group: 'com.github.wumpz', name: 'diffutils', version: '2.2'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'me.tongfei:progressbar:0.5.5'
    compile files("${System.getProperty('java.home')}/../lib/tools.jar")
    compile "org.jetbrains.teamcity:serviceMessages:9.1.6"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

node {
    version = '10.12.0'
    yarnVersion = '1.10.1'
    download = true
    nodeModulesDir = file("${project.projectDir}/js")
}

clean.dependsOn yarn_cache_clean

task yarnBuild(type: YarnTask) {
    dependsOn yarn_install
    args = ['run', 'build']
}

jar {
    dependsOn(yarnBuild)

    from('js/dist') {
        include 'app.js'
        include '2da2b42ac8b23e24cd2b832c22626baf.gif'
        into 'org/jetbrains/distcmp/js'
    }

    from('js/index.html') {
        into 'org/jetbrains/distcmp/js'
    }
}

mainClassName = 'org.jetbrains.distcmp.MainKt'